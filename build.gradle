apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'org.sonarqube'
apply plugin: 'nebula.rpm'
apply plugin: 'nebula.deb'
apply plugin: "com.github.hierynomus.license"
apply plugin: 'org.asciidoctor.convert'
apply plugin: 'com.jfrog.bintray'
//apply plugin: 'nebula.lint'

def _vendor = 'Dell'
def _license = 'Apache License 2.0'
def _group = ''
def _url = 'http://support.dell.com'
def _arch = 'x86_64'
def _os = 'LINUX'
def _release = 1
def _sourcePaths=[]
def apiVersion = ''

def getCurrentTimestamp(){
  new Date().format("MM-dd-yyyy_hh-mm")
}

def updateVersionProperties() {
    def buildInfoFile = file('src/main/resources/buildInfo.properties')
    String buildInfoProp = buildInfoFile.getText('UTF-8')
    println "updating version to '${version}' in ${buildInfoFile}"
    buildInfoProp = buildInfoProp.replaceAll(/buildInfo\.version=.*/, "buildInfo.version=${version}")
    buildInfoFile.write(buildInfoProp, 'UTF-8')
    println "updating docker tag to '${dockerTag}' in ${buildInfoFile}"
    buildInfoProp = buildInfoProp.replaceAll(/buildInfo\.tag=.*/, "buildInfo.tag=${dockerTag}")
    buildInfoFile.write(buildInfoProp, 'UTF-8')
    println "updating build date to '${getCurrentTimestamp()}' in ${buildInfoFile}"
    buildInfoProp = buildInfoProp.replaceAll(/buildInfo\.date=.*/, "buildInfo.date=${getCurrentTimestamp()}")
    buildInfoFile.write(buildInfoProp, 'UTF-8')
}

sourceCompatibility = 1.8
version = "${version}"
dockerTag = "${dockerTag}"
group = 'com.dell.isg.smi'
apiVersion = 'api-'+"${version}"

//check.dependsOn integrationTest
//integrationTest.mustRunAfter test

buildscript {
	ext {
		springVersion = '4.3.6.RELEASE'
    	springBootVersion = '1.5.1.RELEASE'
    	springCloudVersion = 'Camden.SR5'
    	generatedAsciidoc = file("${buildDir}/docs/apidocs/asciidoc/generated")
	}
   repositories {
    mavenLocal()
    mavenCentral()
	maven {
		url "https://plugins.gradle.org/m2/"

	}
  }
  dependencies {
  	classpath "io.spring.gradle:dependency-management-plugin:1.0.2.RELEASE"
    classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
	classpath(group: 'com.netflix.nebula', name: 'gradle-ospackage-plugin', version: '4.4.0' )
	classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.2.1"
	classpath "gradle.plugin.nl.javadude.gradle.plugins:license-gradle-plugin:0.13.1"
	//classpath 'com.netflix.nebula:gradle-lint-plugin:latest.release'
	classpath 'org.asciidoctor:asciidoctor-gradle-plugin:1.5.3'
    classpath 'org.asciidoctor:asciidoctorj-pdf:1.5.0-alpha.15'
    classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
  }
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
	}
}

//gradleLint {
//  rules  = ['dependency-parentheses','overridden-dependency-version','duplicate-dependency-class', 'all-dependency','unused-dependency']
//}

sonarqube {
    properties {
        property "sonar.projectName", "rackhd :: smi-service-virtualnetwork"
        property "sonar.projectKey", "${group}:service-virtualnetwork"
    }
}

springBoot {
    executable = true
}

jar {
    manifest {
        attributes 'Implementation-Title': 'VirtualNetwork MicroService',
                   'Implementation-Version': version
    }
    baseName = 'service-virtualnetwork'
}

repositories {
	mavenCentral()
    mavenLocal()
}

sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integration-test/java')
        }
        resources.srcDir file('src/integration-test/resources')
    }
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

allprojects {
    updateVersionProperties()
}

dependencies {
	//compile project(':virtualnetwork')
	compile 'com.dell.isg.smi:commons-elm:1.0.76'
	compile 'com.dell.isg.smi:commons-utilities:1.0.26'
    compile 'com.dell.isg.smi:virtualnetwork:1.0.89'

	compile "org.springframework.boot:spring-boot-starter-web"
	compile "org.springframework.boot:spring-boot-starter-tomcat"
    compile "org.springframework.boot:spring-boot-starter-actuator"
	compile "org.springframework.boot:spring-boot-starter-security"
	
	compile "org.springframework.boot:spring-boot-starter-data-jpa"
    compile group: 'org.hibernate.javax.persistence', name: 'hibernate-jpa-2.1-api', version: '1.0.0.Final'

	compile "org.springframework.cloud:spring-cloud-starter-consul-all"

	compile "org.springframework.security.oauth:spring-security-oauth2"

    compile "org.apache.commons:commons-lang3:3.5"
    compile "org.postgresql:postgresql:9.4.1212"

	compile group: 'io.springfox', name: 'springfox-swagger2', version: '2.6.1'
	compile group: 'io.springfox', name: 'springfox-swagger-ui', version: '2.6.1'
	testCompile group: 'io.springfox', name:'springfox-staticdocs', version: '2.6.1'
	testCompile group: 'junit', name: 'junit', version: '4.12'
	testCompile 'org.springframework.boot:spring-boot-starter-test'
	testCompile group: 'com.h2database', name: 'h2', version: '1.4.196'
	integrationTestCompile("org.springframework.boot:spring-boot-test-autoconfigure")
	integrationTestCompile 'org.assertj:assertj-core:3.0.0'
}

test {
    systemProperties 'property': 'value'
    systemProperty 'staticdocs.outputDir', generatedAsciidoc
}

tasks.withType(Test) {
    reports.html.destination = file("${reporting.baseDir}/${name}")
}

license {
        header rootProject.file('license-template')
        includes(["**/*.java", "**/*.properties"])
        strictCheck true
        ignoreFailures true
        ext.year = Calendar.getInstance().get(Calendar.YEAR)
    	ext.name = 'DELL Inc.'
}

test {
    systemProperties 'property': 'value'
}


uploadArchives {
    repositories {
       flatDir {
           dirs 'repos'
       }
    }
}

task integrationTest(type: Test) {
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
    outputs.upToDateWhen { false }
}

task generateDockerRpm(type: Rpm){
	destinationDir = project.file('build/distributions')
	destinationDir.mkdirs()

	vendor = _vendor
	license = _license
	group = _group
	summary = 'Exported service-virtualnetwork image'
	description = 'Places the docker container tar.gz into tmp the directory'
	url = _url

	packageName = 'dell-smi-docker-service-virtualnetwork'
	release = _release
	arch = _arch
	os = _os

    from fileTree( "tmp" ), {
        include "**/*"
        fileMode = 0755
	    user = "root"
	    addParentDirs = false
	    into  '/tmp'
    }

	from fileTree( "pkg/docker" ), {
        include "launch.sh"
        fileMode = 0755
	    //user = "root"
	    //addParentDirs = false
	    into  '/tmp'
    }
}

task generateRpm(type: Rpm){
	destinationDir = project.file('build/distributions')
	destinationDir.mkdirs()

	vendor = _vendor
	license = _license
	group = _group
	summary = 'Installs the Dell Virtual Network spring boot application.'
	description = 'The Dell Virtual Network spring boot application.'
	url = _url

	packageName = 'dell-smi-service-virtualnetwork'
	release = _release
	arch = _arch
	os = _os

	from fileTree( "build/libs" ), {
        include "*.jar"
        fileMode = 0755
		user = "root"
		addParentDirs = false
	    into  '/opt/dell/smi/service-virtualnetwork'
    }

	from fileTree("./"), {
        include "application.yml"
        fileMode = 0755
		//user = "root"
		//addParentDirs = true
	    into  '/opt/dell/smi/service-virtualnetwork'
    }

	from fileTree( "pkg" ), {
        include "dell-virtualnetwork.service"
		fileMode = 0777
	    into  '/etc/systemd/system'
    }
	link('/etc/systemd/system/multi-user.target.wants/dell-virtualnetwork.service', '/etc/systemd/system/dell-virtualnetwork.service')
}

task generateDeb(type: Deb){
	destinationDir = project.file('build/distributions')
	destinationDir.mkdirs()

	vendor = _vendor
	license = _license
	group = _group
	summary = 'Installs the Dell Virtual Network spring boot application.'
    description = 'The Dell Virtual Network spring boot application.'
	url = _url

	packageName = 'dell-smi-service-virtualnetwork'
	release = _release
	arch = 'all'
	os = _os

	from fileTree( "build/libs" ), {
        include "*.jar"
        fileMode = 0755
		user = "root"
		addParentDirs = true
	    into  '/opt/dell/smi/service-virtualnetwork'
    }

	//from fileTree("./"), {
    //    include "application.yml"
    //   fileMode = 0755
		//user = "root"
		//addParentDirs = true
	//    into  '/opt/dell/smi/service-virtualnetwork'
    //}

	from fileTree("./"), {
        include "application.yml.secure"
        fileMode = 0755
		//user = "root"
		//addParentDirs = true
	    into  '/opt/dell/smi/service-virtualnetwork'
		rename ('application.yml.secure', 'application.yml')
    }



	from fileTree( "pkg" ), {
        include "dell-virtualnetwork.service"
		fileMode = 0777
	    into  '/etc/systemd/system'
    }
	link('/etc/systemd/system/multi-user.target.wants/dell-virtualnetwork.service', '/etc/systemd/system/dell-virtualnetwork.service')
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}

eclipse {
    project{
    	buildCommand 'org.eclipse.buildship.core.gradleprojectbuilder'
    		
		// assign natures in a groovy fashion:
		natures = ['org.eclipse.buildship.core.gradleprojectnature']
    }
    
    classpath {
       downloadSources=true
    }
}


task buildSourcePathsArray(dependsOn: 'eclipseClasspath' ){
	doLast{
		def classpathFile = file('.classpath')
		if( classpathFile.exists() ) {
			def cpXml = new XmlParser().parse(classpathFile)
			cpXml.classpathentry.each {
				if('lib' == it.@kind ) {
					if( it.@sourcepath == null ){
						println 'missing source jar for:' + it.@path
					}
					else{
					 	_sourcePaths.add(it.@sourcepath)
					}
				}
			}
		}
		 
		def arrayLength = _sourcePaths.size()
		println 'sourcePaths length is:' + arrayLength
	}
}


task dependencySourcesZip(type: Zip, dependsOn: 'buildSourcePathsArray' ) {
	destinationDir = project.file('build/distributions')
	destinationDir.mkdirs()
	baseName = "smi-service-virtualnetwork-dependency-sources"
	version = "${dockerTag}"
	outputs.upToDateWhen { false }
 
    from _sourcePaths
}

asciidoctor {
    dependsOn test
    sources {
        include 'api-*.adoc'
    }
    backends = ['pdf']
    attributes = [
        doctype: 'book',
        toc: 'left',
        toclevels: '2',
        numbered: '',
        sectlinks: '',
        sectanchors: '',
        hardbreaks: '',
        generated: generatedAsciidoc
    ]
}

bintrayUpload.dependsOn asciidoctor

bintray {
	user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
	key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    filesSpec {
       from file(asciidoctor.outputDir.path + '/pdf')
       into '.'
    }
    dryRun = false
    publish = true
    override = false
    pkg {
		repo = "${bintrayRepo}"
		name = apiVersion
		userOrg = "${bintrayUserOrg}"
		desc = 'Swagger API for Virtual Network Service.'
		licenses = ['Apache-2.0']
		vcsUrl = "${bintrayVcsUrl}"
		publicDownloadNumbers = true
		version {
			name = apiVersion
			desc = 'apidoc'
			vcsTag = 'master'
		}
	}
}
